/*
 * Copyright (C) 2018-2021 Felix Wiemuth and contributors (see CONTRIBUTORS.md)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.31'
}

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def gitDescribe = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty', '--match', '"not a tag"'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        // Release branch specific variables
        applicationId "felixwiemuth.simplereminder.dev"
        def releaseChannel = 'dev'
        def versionCodeReleaseChannel = 1

        // Version variables
        def versionCodeMajor = 0
        def versionCodeMinor = 9
        def versionCodePoint = 11

        // Sub version for release channel (only used on some release channels)
        def versionCodeSub = 0

        versionCode = new Integer(
                10**0 * versionCodeSub // 2 digits
              + 10**2 * versionCodeReleaseChannel // 1 digit
              + 10**3 * versionCodePoint // 2 digits
              + 10**5 * versionCodeMinor // 3 digits
              + 10**8 * versionCodeMajor // 2 digits
        //      10^10 is the reserve digit which can only be 0 or 1 with full range below
        //      Max value allowed by Google Play: 2100000000
        )

        def gitDescription = "${gitDescribe()}"

        versionName = versionCodeMajor + "." + versionCodeMinor + "." + versionCodePoint + "-" + releaseChannel + "-" + gitDescription

        minSdkVersion 16
        targetSdkVersion 31

        archivesBaseName = "SimpleReminder_" + versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'felixwiemuth.simplereminder'
}

dependencies {
    def acraVersion = '5.8.4'
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation 'de.cketti.library.changelog:ckchangelog:1.2.2'
    testImplementation 'junit:junit:4.13.2'
}
